source "proxmox-iso" "rocky8" {
  // Proxmox Builder Settings
  proxmox_url              = "https://${local.pve_host}:8006/api2/json"
  username                 = "${local.pve_api_token_id}"
  token                    = "${local.pve_api_token_secret}"
  node                     = "pve1"
  insecure_skip_tls_verify = local.pve_tlsInsecure
  task_timeout             = "10m"

  // ISO / Kickstart / Cloud-Init Settings
  iso_url          = "https://download.rockylinux.org/pub/rocky/8/isos/x86_64/Rocky-x86_64-boot.iso"
  iso_checksum     = "fe77cc293a2f2fe6ddbf5d4bc2b5c820024869bc7ea274c9e55416d215db0cc5"
  iso_storage_pool = "local"
  unmount_iso      = true
  additional_iso_files {
    device           = "ide0"
    cd_files         = [ "./templates/files/ks.cfg" ]
    cd_label         = "OEMDRV"
    iso_storage_pool = "local"
    unmount          = true
  }

  // SSH Communicator Settings
  ssh_username = "root"
  ssh_password = "packer1234"
  ssh_timeout  = "60m"

  // VM Settings
  vm_name              = "template-rocky8"
  vm_id                = 9999
  template_name        = "template-rocky8"
  template_description = <<-EOT
    Rocky Linux 8 base template.

    Generated by Packer on ${timestamp()}.
  EOT

  memory     = 2048
  cores      = 1
  sockets    = 1
  os         = "l26"
  qemu_agent = true

  scsi_controller     = "virtio-scsi-pci"
  disks {
    type              = "scsi"
    disk_size         = "8G"
    storage_pool      = "local-zfs"
    storage_pool_type = "zfspool"
    format            = "raw"
  }
  cloud_init              = true
  cloud_init_storage_pool = "local-zfs"

  network_adapters {
    bridge   = "vmbr0"
    model    = "virtio"
    firewall = true
  }
}

build {
  sources = ["source.proxmox-iso.rocky8"]
  provisioner "shell" {
    inline = [
      "sed -i 's/PasswordAuthentication yes/PasswordAuthentication no/g' /etc/ssh/sshd_config",
      "passwd -d root",
      "passwd -l root",
      "rm -f /etc/cloud/cloud-init.disabled"
    ]
  }
}
